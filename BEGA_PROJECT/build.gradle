plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.11'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    
    // Standard Lombok (managed by Spring Boot)
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'
    implementation 'com.oracle.database.nls:orai18n:21.9.0.0'
    // OCI Wallet Support
    implementation 'com.oracle.database.security:oraclepki:21.9.0.0'
    implementation 'com.oracle.database.security:osdt_cert:21.9.0.0'
    implementation 'com.oracle.database.security:osdt_core:21.9.0.0'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    
    // AWS S3
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.1'
    
    // Image resizing
    implementation 'net.coobird:thumbnailator:0.4.20'
    implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
    // WebP support
    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.10.1'
    implementation 'com.twelvemonkeys.imageio:imageio-core:3.10.1'
    implementation 'com.github.gotson:webp-imageio:0.2.2'

    // Netty macOS DNS fix
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.118.Final:osx-aarch_64'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    
    // WebFlux (WebClient)
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Caffeine Cache
    implementation 'com.github.ben-manes.caffeine:caffeine'
    
    // JobRunr (Spring Boot 3)
    implementation 'org.jobrunr:jobrunr-spring-boot-3-starter:7.2.0'
    
    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.flywaydb:flyway-database-oracle'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
	mateTest {
		java {
			srcDirs = ['src/test/java']
			include 'com/example/mate/**'
		}
		resources {
			srcDirs = ['src/test/resources']
		}
		compileClasspath += sourceSets.main.output + configurations.testCompileClasspath
		runtimeClasspath += output + sourceSets.main.output + configurations.testRuntimeClasspath
	}
}

configurations {
	mateTestImplementation.extendsFrom testImplementation
	mateTestRuntimeOnly.extendsFrom testRuntimeOnly
	mateTestCompileOnly.extendsFrom testCompileOnly
	mateTestAnnotationProcessor.extendsFrom testAnnotationProcessor
}

springBoot {
    mainClass = 'com.example.BegaProjectApplication'
}

tasks.register('mateTest', Test) {
	group = 'verification'
	description = 'Runs only mate-domain tests from src/test/java/com/example/mate/**'
	testClassesDirs = sourceSets.mateTest.output.classesDirs
	classpath = sourceSets.mateTest.runtimeClasspath
}

tasks.named('compileMateTestJava') {
	dependsOn tasks.named('classes')
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()

	// Stabilize local/CI runs for heavy integration suites.
	maxParallelForks = 1
	failFast = false
	jvmArgs '-Dfile.encoding=UTF-8'
	systemProperty 'file.encoding', 'UTF-8'

	reports {
		junitXml.required = true
		junitXml.outputPerTestCase = true
		junitXml.mergeReruns = true
		junitXml.includeSystemOutLog = false
		junitXml.includeSystemErrLog = false
	}

	testLogging {
		events "failed", "skipped"
		exceptionFormat "short"
		showStandardStreams = false
	}
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-parameters"]
}
